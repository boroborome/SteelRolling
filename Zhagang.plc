TITLE=
Network 1
LD     SM0.1
LPS
  MOVW   +20, BilletTimeTable
  AENO
  FILL   0, BilletTimeTableSize, 20

LRD
  FILL   0, HistoryData, 20
  AENO
  MOVD 0,HistoryIndex
LPP
  MOVB   1, SB0

Network 2
LD     I0.7
=      Q1.0
TONR   T0, 32767

Network 3 //  网络题目 (单行)
//  测量长度
LDN    I0.7
MOVW   T0, AC1
AW>    AC1, 50
ATT    AC1, BilletTimeTable
R      T0, 1

Network 4
//  空闲状态
LD     S0.0
MOVB   1, QB0                      // 空闲亮灯
AENO
LPS
  MOVW   BilletTimeTableSize, AC1
  AENO
  AW<>   AC1, 0
  FIFO   BilletTimeTable, AC1
  AENO
  AW>    AC1, 50
  // CurrentBilletTime
  LPS
    ITD    AC1, AC1
    AENO
    DTR    AC1, CurrentBilletTime

  // CurrentBilletLength=CurrentBilletTime*BilletSpeed
  LRD
    ITD    BilletSpeed, AC1
    AENO
    DTR    AC1, AC1
    AENO
    MOVR   CurrentBilletTime, CurrentBilletLength
    AENO
    *R     AC1, CurrentBilletLength

  // DeltaK1Gap=(CurrentBilletLength-ExpectBilletLength)
  // *BilletWidth*BilletHeight/(TargetSteelWidth*TargetSteelLength)
  LRD
    MOVR   CurrentBilletLength, AC1
    AENO
    ITD    ExpectBilletLength, AC2
    AENO
    DTR    AC2, AC2
    AENO
    -R     AC2, AC1
    AENO
    ITD    BilletWidth, AC2
    AENO
    DTR    AC2, AC2
    AENO
    *R     AC2, AC1
    AENO
    ITD    BilletHeight, AC2
    AENO
    DTR    AC2, AC2
    AENO
    *R     AC2, AC1
    AENO
    ITD    TargetSteelWidth, AC2
    AENO
    DTR    AC2, AC2
    AENO
    /R     AC2, AC1
    AENO
    ITD    TargetSteelLength, AC2
    AENO
    DTR    AC2, AC2
    AENO
    /R     AC2, AC1
    AENO
    MOVR   AC1, DeltaK1Gap

  // TargetK1Gap=ZeroK1Gap+DeltaK1Gap
  LRD
    MOVR   ZeroK1Gap, AC1
    AENO
    +R     DeltaK1Gap, AC1
    AENO
    MOVR   AC1, TargetK1Gap

  // MonitorOutputVoltage=TargetK1Gap/MonitorGapRate
  LRD
    MOVR   TargetK1Gap, MonitorOutputVoltage
    AENO
    /R     MonitorGapRate, MonitorOutputVoltage

  // MonitorOutput=MonitorOutputVoltage/(10/32767)
  LRD
    MOVR   MonitorOutputVoltage, AC1
    AENO
    *R     32767.0, AC1
    AENO
    /R     10.0, AC1
    AENO
    MOVR   AC1, MonitorOutput
  LRD
    MOVD &HistoryData,AC1
    AENO
    MOVD HistoryIndex,AC0
    AENO
    *D 28,AC0
    AENO
    +D AC0,AC1
    AENO
    BMD CurrentBilletTime,*AC1,6
    AENO
    INCD HistoryIndex
    AENO
    AD> HistoryIndex,10
      MOVD 0,HistoryIndex
  LPP
    MOVB   2, SB0                      // 调整状态

LPP
  AW= BilletTimeTableSize,0
  LPS
    // MonitorOutputVoltage=ZeroK1Gap/MonitorGapRate
    MOVR   ZeroK1Gap, MonitorOutputVoltage
    AENO
    /R     MonitorGapRate, MonitorOutputVoltage

  // MonitorOutput=MonitorOutputVoltage/(10/32767)
  LPP
    MOVR   MonitorOutputVoltage, AC1
    AENO
    *R     32767.0, AC1
    AENO
    /R     10.0, AC1
    AENO
    MOVR   AC1, MonitorOutput



Network 5
// 等待状态
LD     S0.1
MOVB   3, QB0                      //等待状态灯
//模拟运行状态
TON    T103, +100

Network 6
// 运行状态
//模拟监测电流减退
LD     T103
R      T103, 1
MOVB   1, SB0
MOVB   1, QB0
